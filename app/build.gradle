apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven {
        // for com.neenbedankt.bundles:bundles
        url  'https://github.com/zaki50/bundles_maven_repo/raw/master/maven_repo'
    }
}

dependencies {

    compile "com.nineoldandroids:library:${nineOldAndroidsVersion}"

    compile "com.android.support:support-v13:${supportPackageVersion}"
    compile "com.android.support:appcompat-v7:${supportPackageVersion}"
    compile "com.android.support:design:${supportPackageVersion}"
    compile "com.android.support:percent:${supportPercentPackageVersion}"
    //compile "com.android.support:leanback-v17:${supportPackageVersion}"
    //compile "com.android.support:recyclerview-v7:${supportPackageVersion}"
    //compile "com.android.support:gridlayout-v7:${supportPackageVersion}"
    //compile "com.android.support:mediarouter-v7:${supportPackageVersion}"
    //compile "com.android.support:palette-v7:${supportPackageVersion}"
    //compile "com.android.support:cardview-v7:${supportPackageVersion}"
    compile "com.android.support:support-annotations:${supportPackageVersion}"

    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    apt "com.jakewharton:butterknife:${butterKnifeVersion}"

    // Butter Knife ではなく、 Android Annotations を使う場合はこちら
    //compile "org.androidannotations:androidannotations-api:${project.androidAnnotationsVersion}"
    //apt "org.androidannotations:androidannotations:${project.androidAnnotationsVersion}"

    compile "com.squareup.dagger:dagger:${project.daggerVersion}"
    apt "com.squareup.dagger:dagger-compiler:${project.daggerVersion}"

    compile "net.vvakame:jsonpullparser-core:${project.jppVersion}"
    apt "net.vvakame:jsonpullparser-apt:${project.jppVersion}"

    compile "de.greenrobot:eventbus:${eventbusVersion}"

    compile "com.squareup.okio:okio:${okioVersion}"
    compile "com.squareup.okhttp:okhttp:${okhttpVersion}"
    compile "com.squareup.okhttp:okhttp-urlconnection:${okhttpVersion}"

    compile "com.squareup.retrofit:retrofit:${retrofitVersion}"

    compile "io.reactivex:rxjava:${rxjavaVersion}"
    compile "io.reactivex:rxjava:${rxandroidVersion}"

    compile "com.neenbedankt.bundles:bundles:${bundlesVersion}"

    compile "se.emilsjolander:intentbuilder-api:${intentBuilderVersion}"
    apt "se.emilsjolander:intentbuilder-compiler:${intentBuilderVersion}"

    // Google Play Services 全体を取り込む場合はこちらを。一部機能だけ取り込む場合はコメントアウトし、下から必要な物を有効化
    /*
     * proguard をかけないとメソッド数上限に当たる可能性が高い。
     * 分割版を使うか、 android.buildTypes.debug.multiDexEnabled を true にするのがおすすめ
     */
    //compile "com.google.android.gms:play-services:${playServicesVersion}"

    // Google Play Services の機能分割版
    // Google Plus
    //compile "com.google.android.gms:play-services-plus:${playServicesVersion}"
    // Google Account Login
    //compile "com.google.android.gms:play-services-identity:${playServicesVersion}"
    // Google Actions
    // Google Analytics
    // Google Cloud Messaging
    //compile "com.google.android.gms:play-services-base:${playServicesVersion}"
    // Google App Indexing
    //compile "com.google.android.gms:play-services-appindexing:${playServicesVersion}"
    // Google Analytics
    //compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"
    // Google Cast
    //compile "com.google.android.gms:play-services-cast:${playServicesVersion}"
    // Google Cloud Messaging
    //compile "com.google.android.gms:play-services-gcm:${playServicesVersion}"
    // Google Drive
    //compile "com.google.android.gms:play-services-drive:${playServicesVersion}"
    // Google Fit
    //compile "com.google.android.gms:play-services-fitness:${playServicesVersion}"
    // Google Activity Recognition
    //compile "com.google.android.gms:play-services-location:${playServicesVersion}"
    // Google Maps
    compile "com.google.android.gms:play-services-maps:${playServicesVersion}"
    // Google Mobile Ads
    //compile "com.google.android.gms:play-services-ads:${playServicesVersion}"
    // Google Nearby
    //compile "com.google.android.gms:play-services-nearby:${playServicesVersion}"
    // Google Panorama Viewer
    //compile "com.google.android.gms:play-services-panorama:${playServicesVersion}"
    // Google Play Game services
    //compile "com.google.android.gms:play-services-games:${playServicesVersion}"
    // SafetyNet
    //compile "com.google.android.gms:play-services-wallet:${playServicesVersion}"
    // Google Wallet
    //compile "com.google.android.gms:play-services-wallet:${playServicesVersion}"
    // Android Wear
    //compile "com.google.android.gms:play-services-wearable:${playServicesVersion}"


    // http://googlemaps.github.io/android-maps-utils/
    compile "com.google.maps.android:android-maps-utils:${androidMapsUtils}"

    compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
    compile "commons-io:commons-io:${commonsIoVersion}"

    compile("com.google.api-client:google-api-client-android:${googleApiClientVersion}") {
        exclude module: 'xpp3'
        exclude module: 'httpclient'
        exclude module: 'junit'
        exclude module: 'android'
    }

    // libs/ にある *.jar を一括で追加する
    compile fileTree(dir: 'libs', include: '*.jar')

    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"
    debugCompile "com.facebook.stetho:stetho-okhttp:${stethoVersion}"

    debugCompile project(path: ':library', configuration: 'libDebug')
    releaseCompile project(path: ':library', configuration: 'libRelease')

    androidTestCompile "com.android.support.test:testing-support-lib:${testingSupportPackageVersion}"

    // android wear 用のアプリを handheld アプリに埋め込む
    wearApp project(':wear')
}

// for Android Annotations
android.applicationVariants.all { variant ->
    variant.javaCompile.options.compilerArgs += [
            '-AandroidManifestFile=' + variant.outputs.get(0).processManifest.manifestOutputFile
    ]
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21

        // support renderscript を使う場合
        // renderscriptTargetApi 21
        // renderscriptOptimLevel 3
        // renderscriptSupportModeEnabled true
        // renderscriptNdkModeEnabled false

        versionCode 1
        versionName '0.0.1'

        // test support libの JUnitRunner
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // ndk を使用する際の設定。
        // local.properties に ndk.dir=/path/to/ndk の記述を行う必要あり
        // ndk 関連のファイルは例えば app/src/main/jni/ の下に置く
        // 使用するクラスで System.loadLibrary("native_library"); するのをわすれずに

        // ndk {
        //     moduleName "native_library"
        //     stl "gnustl_shared"
        //     abiFilters 'all'
        //     //abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64', 'mips', 'mips64'
        //     ldLibs "log"
        // }
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        // exclude duplicate files in apache commons
        // pickFirst なら最初に見つけたやつが apk に入る
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'

        // for testing support lib
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        // debug {
        //     storeFile file("debug.keystore")
        // }

        release
    }

    aaptOptions {
        // 最近の PNG processor が生成するファイルがGB端末上でクラッシュを引き起こすとのことなので、2系をサポートするなら以下の設定を使う
        // useAaptPngCruncher = true

        // android wear 用の apk を asset に置く場合に、圧縮を行わないようにするための設定
        // see http://developer.android.com/training/wearables/apps/packaging.html
        noCompress "apk"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // minifyEnabled true の指定必須
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile file('proguard-rules.pro')

            multiDexEnabled false

            jniDebuggable false
            renderscriptDebuggable false

            // gradle.properties にリリース署名用の設定があれば署名を有効化します。設定のサンプルとして gradle.properties.forReleaseSigning を置いたので参考にしてください。
            if (project.hasProperty('storeFile')) {
                signingConfig signingConfigs.release
            }

            //API キーは Google APIs Console https://code.google.com/apis/console から取得する。
            // 詳しくは https://developers.google.com/maps/documentation/android/start?hl=ja#the_google_maps_api_key を参照。
            // ex. $ keytool -list -v -keystore ~/.android/release.jks -alias release
            manifestPlaceholders = [maps_v2_apikey: 'リリース用の証明書に対応するAPIキーをここに書く']
        }

        debug {

            multiDexEnabled false

            jniDebuggable false
            renderscriptDebuggable false

            manifestPlaceholders = [maps_v2_apikey: 'デバッグ用の証明書に対応するAPIキーをここに書く']
        }
    }
}

if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}

// IDE からのビルドと、コマンドラインからのビルドで処理を分けたい場合の書き方
// Android Studio からのビルドでは、コマンドラインオプションに -Pandroid.injected.invoked.from.ide=true が渡される
def isIdeBuild = project.properties['android.injected.invoked.from.ide']
if (isIdeBuild == null || isIdeBuild.equalsIgnoreCase("true")) {
    // do something for ide build
}

